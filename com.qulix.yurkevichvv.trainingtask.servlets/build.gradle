import com.bmuschko.gradle.tomcat.tasks.TomcatRun;
import com.bmuschko.gradle.tomcat.tasks.TomcatStop;

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.7.0'
        classpath 'com.bmuschko:gradle-cargo-plugin:2.9.0'
    }
}

plugins {
    id "java"
    id "war"
}

group "com.qulix.yurkevichvv.trainingtask"
version "1.0-SNAPSHOT"

apply plugin: "com.bmuschko.tomcat"
apply plugin: "com.bmuschko.cargo"

repositories {
    mavenCentral()
}

ext {
    junitVersion = "5.9.0"
    tomcatStopPort = 8090
    tomcatHttpPort = 2134
    tomcatHttpsPort = 18080
    tomcatAjpPort = 9091
    tomcatStopKey = "servletStopKey"
}

dependencies {
    implementation project(":com.qulix.yurkevichvv.trainingtask.model")
    compileOnly "javax.servlet:javax.servlet-api:4.0.1"
    implementation "org.hsqldb:hsqldb:2.7.0"
    compileOnly("javax:javaee-web-api:8.0.1")
    implementation "org.apache.wicket:wicket:9.9.1"

    implementation group: "jstl", name: "jstl", version: "1.2"
    implementation group: "taglibs", name: "standard", version: "1.1.2"
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    def tomcatVersion = "9.0.56"
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:9.0.0.M6",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

    def cargoVersion = '1.9.10'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
            "org.codehaus.cargo:cargo-ant:$cargoVersion"

}

test {
    useJUnitPlatform()
}

tomcat {
    httpsPort = tomcatHttpsPort
    httpProtocol = "org.apache.coyote.http11.Http11Nio2Protocol"
    ajpProtocol = "org.apache.coyote.http11.Http11Nio2Protocol"
}
cargo{
    containerId = "tomcat8x"
    port = tomcatHttpPort

    deployable {
        file = file("${buildDir}/libs/com.qulix.yurkevichvv.trainingtask.servlets-1.0-SNAPSHOT.war")
        context = "/servlets"
    }

    local {
        homeDir= file("${rootDir}/extract/apache-tomcat-9.0.59/apache-tomcat-9.0.59")
        outputFile = new File('logs/apache-tomcat-9.0.59/server.log')
        rmiPort = 4444
    }

    remote {
        hostname = 'localhost'
        username = 'admin'
        password = 'admin'
    }
}

task startServer(type: TomcatRun) {
    httpPort = tomcatHttpPort
    httpsPort = tomcatHttpsPort
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
    ajpPort = tomcatAjpPort
    contextPath = 'servlet'
    reloadable = true
    daemon = true
    dependsOn war
}

task stopServer(type: TomcatStop){
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
}

cargoDeployRemote.dependsOn build
cargoRedeployRemote.dependsOn build