import com.bmuschko.gradle.tomcat.tasks.TomcatRun;
import com.bmuschko.gradle.tomcat.tasks.TomcatStop;

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.bmuschko:gradle-tomcat-plugin:2.7.0"
        classpath "com.bmuschko:gradle-cargo-plugin:2.9.0"
    }
}

plugins {
    id "java"
    id "war"
}

group "com.qulix.yurkevichvv.trainingtask"
version "1.0-SNAPSHOT"

apply plugin: "com.bmuschko.tomcat"
apply plugin: "com.bmuschko.cargo"

repositories {
    mavenCentral()
}

ext {
    junitVersion = "5.9.0"
    tomcatStopPort = 6666
    tomcatStopKey = "wicketStopKey"
    tomcatAjpPort = 1235
    tomcatHttpsPort = 18080
    tomcatHttpPort = 9090
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation project(":com.qulix.yurkevichvv.trainingtask.model")
    implementation "javax.servlet:javax.servlet-api:4.0.1"
    implementation "org.hsqldb:hsqldb:2.5.1"
    compileOnly("javax:javaee-web-api:8.0.1")
    implementation "org.apache.wicket:wicket:9.9.1"
    implementation "org.apache.wicket:wicket-core:9.9.1"
    implementation "org.apache.wicket:wicket-extensions:9.9.1"
    testImplementation "junit:junit:4.11"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"

    def tomcatVersion = "9.0.59"
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:9.0.0.M6",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

    def cargoVersion = "1.9.8"
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
            "org.codehaus.cargo:cargo-licensed-dtds:$cargoVersion",
            "org.codehaus.cargo:cargo-ant:$cargoVersion"
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        resources {
            srcDirs += ["src/main/java", "src/main/webapp"]
            includes = ["**/*.html", "**/*.css", "**/*.properties"]
        }
    }
}

tomcat {
    httpProtocol = "org.apache.coyote.http11.Http11Nio2Protocol"
    ajpProtocol = "org.apache.coyote.http11.Http11Nio2Protocol"
}

cargo{
    containerId = "tomcat8x"
    port = tomcatHttpPort

    deployable {
        file = file("${buildDir}/libs/com.qulix.yurkevichvv.trainingtask.wicket-1.0-SNAPSHOT.war")
        context = "/wicket"
    }

    local {
        homeDir= file("${rootDir}/extract/apache-tomcat-9.0.59/apache-tomcat-9.0.59")
        outputFile = new File('logs/apache-tomcat-9.0.59/server.log')
        rmiPort = 7777
    }

    remote {
        hostname = 'localhost'
        username = 'admin'
        password = 'admin'
    }
}

task startWicketServer(type: TomcatRun) {
    httpPort = tomcatHttpPort
    ajpPort = tomcatAjpPort
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
    contextPath = "/wicket"
    daemon = true
    reloadable = true
    dependsOn war
}

task stopWicketServer(type: TomcatStop){
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
}

cargoDeployRemote.dependsOn assemble
